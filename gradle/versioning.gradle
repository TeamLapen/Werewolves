project.mod_version = "${project.major_version}.${project.api_version}.${project.minor_version}.${project.patch_version}"
if (hasProperty("release")) {
    project.type = "release"
    project.classification = "public"
    // version is already correct
} else if (hasProperty("beta")) {
    project.type = "beta"
    project.classification = "public"
    project.mod_version = project.mod_version + "-beta." + beta
} else if (hasProperty("alpha")) {
    project.type = "alpha"
    project.classification = "public"
    project.mod_version = project.mod_version + "-alpha+" + getDate()
} else if (hasProperty("testing")) {
    project.type = "test"
    project.classification = "private"
    project.mod_version = project.mod_version + "-test+" + getDate()
} else if (System.getenv().CI) {
    project.type = "test"
    project.classification = "private"
    project.mod_version = project.mod_version + "-test+" + System.getenv().GIT_BRANCH + "." + System.getenv().GIT_COMMIT
} else {
    project.type = "test"
    project.classification = "test"
    project.mod_version = project.mod_version + "-build+" + getDate()
}

version = project.minecraft_version + "-" + project.mod_version

logger.lifecycle "Version " + project.mod_version

def writeVersion() {
    def file = new File("version.txt")
    file.text = project.mod_version
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('YYMMdd-HHmm')

    return formattedDate
}
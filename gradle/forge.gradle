repositories {
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        url = "https://maven.maxanier.de"
    }
    maven {
        url "https://nexus.resourcefulbees.com/repository/maven-public/"
    }
    mavenCentral()
    mavenLocal()
}


dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
    //Vampirism
    implementation fg.deobf("de.teamlapen.vampirism:Vampirism:${project.minecraft_version}-${project.vampirism_version}")
    implementation fg.deobf("de.maxanier.guideapi:Guide-API-VP:${project.guideapi_version}")

    //Utility
    compileOnly fg.deobf("mezz.jei:jei-${project.jei_version}") //API for compile (not POM)
    runtimeOnly fg.deobf("mezz.jei:jei-${project.jei_version}") //Full for runtime (not POM)

    //Mixin preprocessor
    if (System.getProperty("idea.sync.active") != "true") {
        annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    }

    // Terrablender for biome addition
    compileOnly fg.deobf("com.github.glitchfiend:TerraBlender-forge:${project.terrablender_version}")
    // Only enable for testing
//    runtimeOnly fg.deobf("com.github.glitchfiend:TerraBlender-forge:${project.terrablender_version}")

    // Blame for debugging
//    runtimeOnly fg.deobf("com.telepathicgrunt:Blame:${project.blame_version}")
}

minecraft {
    mappings channel: "${project.mcp_channel}".toString(), version: "${project.mcp_mappings}".toString()
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            taskName 'runClient'
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'vampirism_target', 'dev_client'
            workingDirectory project.file('run/client').canonicalPath
            jvmArg '-ea:de.teamlapen...'

            mods {
                werewolves {
                    source sourceSets.main
                }
            }

        }
        client2 {
            parent runs.client
            taskName 'runClient2'
            arg '--username=Dev2'
        }
        server {
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'vampirism_target', 'dev_server'
            workingDirectory project.file('run/server').canonicalPath
            jvmArg '-ea:de.teamlapen...'

            mods {
                werewolves {
                    source sourceSets.main
                }


            }
        }
        data {
            property 'mixin.env.disableRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'vampirism_target', 'dev_data'
            workingDirectory project.file('run/data').canonicalPath
            args '--mod', 'werewolves', '--all', '--output', file('src/generated/resources/'),  '--existing', sourceSets.main.resources.srcDirs[0]
            mods {
                werewolves {
                    source sourceSets.main
                }
            }
        }
    }

}

task downloadCrowdin() {
    ext {
        output = file('build/crowdin_raw.zip')
        update = file('build/crowdin.json')
        id = 'werewolves'
    }
    outputs.upToDateWhen { false }
    onlyIf {
        project.hasProperty('CROWDIN_KEY') && !project.gradle.startParameter.isOffline()
    }
    doLast {
        download {
            src "https://api.crowdin.com/api/project/${id}/export?key=${project.CROWDIN_KEY}&export_translated_only&json"
            dest update
            overwrite true
        }
        if (!update.text.contains('success')) {
            throw new RuntimeException("Crowdin export failed, see ${update} for more info")
        }
        download {
            src "https://api.crowdin.com/api/project/${id}/download/all.zip?key=${project.CROWDIN_KEY}"
            dest output
            overwrite true
        }
    }
}

task crowdin(type: Copy){
    dependsOn downloadCrowdin
    onlyIf {
        !downloadCrowdin.state.skipped
    }
    destinationDir = file('build/translations')
    from(zipTree(downloadCrowdin.output)){
        filter { String line ->
            line.indexOf("\"\"") != -1 ? null : line //Filter empty translations
        }
        filteringCharset = 'UTF-8'
        exclude { it.isDirectory() }
        rename { it.toLowerCase() }//Minecraft needs it lowercase.
    }
}



jar {
    dependsOn 'crowdin'
    finalizedBy('reobfJar')

    //Keep the jar as clean as possible
    includeEmptyDirs = false

    from sourceSets.main.output.classesDirs
    from sourceSets.main.output.resourcesDir

    from{
        fileTree('build/translations').matching{
            exclude "**/en_us.json" //Don't override local source file
        }
    }



    manifest {
        attributes([
                "FMLAT": "accesstransformer.cfg",
                "Specification-Title": "werewolves",
                "Specification-Vendor": "cheaterpaul",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor" :"cheaterpaul",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "werewolves.mixins.json"
        ])
    }

}

//Replace strings in IDEA folders as well
def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def intoTargets = ["$rootDir/out/production/resources/", "$rootDir/out/production/${project.name}.main/", "$rootDir/bin/main/"]
def replaceProperties = ['version': project.mod_version, 'mcversion': minecraft_version, 'forge_version': forge_version, 'jei_version': jei_version, 'guideapi_version': guideapi_version, 'vampirism_version': vampirism_version]
processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }

    intoTargets.each { target ->
        if (file(target).exists()) {
            copy {
                from(sourceSets.main.resources) {
                    include resourceTargets
                    expand replaceProperties
                }
                into target
            }
        }
    }
    exclude '**/Thumbs.db'

}

apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "werewolves.refmap.json"
    config 'werewolves.mixins.json'

    debug.verbose = true
    debug.export = true
}